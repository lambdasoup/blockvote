/*
 * Copyright 2017 mh@lambdasoup.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 26
    buildToolsVersion buildToolsVersion

    sourceSets {
        main {
            proto {
                srcDir '../../service'
            }
        }
    }

    defaultConfig {
        applicationId "com.lambdasoup.blockvote"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode makeVersionCode(0)
        versionName makeVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation project(":android:base")

    // google/firebase
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"
    implementation "com.google.android.gms:play-services-base:$playServicesVersion"


    // android support libs
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"

    // architecture components
    implementation "android.arch.lifecycle:runtime:$archComponentsVersion"
    implementation "android.arch.lifecycle:extensions:$archComponentsVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archComponentsVersion"

    // protobuf
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'

    // http
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}

apply plugin: 'com.google.gms.google-services'